USERS    TITLE 'USERS     -   DISPLAYS ACTIVE TSO USER IDS'
***********************************************************************
*                                                                     *
*        MODULE NAME = USERS                                          *
*                                                                     *
*        DESCRIPTIVE NAME = USERS   - DISPLAYS ACTIVE TSO USERIDS     *
*                                                                     *
*                                                                     *
*        AUTHOR   =    UNKNOWN                                        *
*                                                                     *
*        FUNCTION =                                                   *
*                                                                     *
*                  THIS ROUTINE DISPLAYS THE NUMBER OF TSO USERS      *
*                  CURRENTLY LOGGED ON AND THEIR USERIDS IN SORTED    *
*                  ORDER.                                             *
*                                                                     *
*        OPERATION =                                                  *
*                                                                     *
*                ESTIMATE STORAGE REQUIRED AND ALLOCATE IT            *
*                EXAMINE CSCB (HANDLE TO CSCB CHAIN IN MS RESIDENT    *
*                DATA AREA) CHAIN AND GET TSO USERIDS. USERIDS ARE    *
*                OBTAINED FROM CHKEY IN CSCB, ALSO CHTRKID =1 FOR     *
*                TSO USERS.                                           *
*                STORE USERIDS IN A TABLE TO BE SORTED LATER ON       *
*                DISPLAY THE NUMBER OF ACTIVE TSO USERS AND THEIR     *
*                 USERIDS                                             *
*                FREE ALLOCATED AREAS                                 *
*                                                                     *
*                REGISTER CONVENTIONS = STANDARD CONVENTIONS.         *
*                   REGISTER  0 WORK REGISTER                         *
*                   REGISTER  1 WORK REGISTER                         *
*                   REGISTER  2 ADDRESS OF CVTMSER                    *
*                   REGISTER  3 ADDRESS OF CVT                        *
*                   REGISTER  4 ADDRESS OF CVTASVT                    *
*                   REGISTERS 5,6,7,8,9,10,11 WORK REGISTERS          *
*                   REGISTER 12 BASE REGISTER                         *
*                   REGISTER 13 ADDRESS OF SAVE AREA                  *
*                   REGISTER 14 RETURN ADDRESS                        *
*                   REGISTER 15 ENTRY POINT ADDRESS                   *
*                                                                     *
*        MODULE TYPE = CP                                             *
*                                                                     *
*               PROCESSOR = ASM                                       *
*                                                                     *
*               MODULE SIZE = 0248 BYTES                              *
*                                                                     *
*               ATTRIBUTES = REENTRANT                                *
*                                                                     *
*        ENTRY POINT  = USERS                                         *
*                                                                     *
*                                                                     *
*        INPUT = NONE                                                 *
*                                                                     *
*        OUTPUT = REGISTER 15 - RETURN CODE = 0                       *
*                                                                     *
*        EXIT - NORMAL = AT PROGRAM END VIA BR  14                    *
*                                                                     *
*               OUTPUT =                                              *
*                    1) NUMBER OF TSO ACTIVE TSO USERS                *
*                    2) USERIDS OF ACTIVE TSO USERS                   *
*                                                                     *
*               RETURN CODE =  0                                      *
*                                                                     *
*                                                                     *
*        EXIT - ERROR = NONE                                          *
*                                                                     *
*               OUTPUT = NONE                                         *
*                                                                     *
*                                                                     *
*        EXTERNAL REFERENCES =                                        *
*                                                                     *
*               ROUTINES = NONE                                       *
*                                                                     *
*                                                                     *
*        MACROS USED =                                                *
*                                                                     *
*             MACRO                   DSNAME                          *
*             -----                   -------                         *
*                                                                     *
*              CVT                    SYS1.AMODGEN                    *
*              IHAASVT                SYS1.AMODGEN                    *
*              IKJCPPL                SYS1.AMODGEN                    *
*              IKJIOPL                SYS1.AMODGEN                    *
*              IEEBASEA               SYS1.AMODGEN                    *
*              IEECHAIN               SYS1.AMODGEN                    *
*              REGISTER               SYS2.MACLIB                     *
*              FREEMAIN               SYS1.MACLIB                     *
*              GETMAIN                SYS1.MACLIB                     *
*              PUTLINE                SYS1.MACLIB                     *
*                                                                     *
*                                                                     *
*        ABEND CODES = NONE                                           *
*                                                                     *
*        RESTRICTIONS = NONE                                          *
*                                                                     *
*        CHANGE ACTIVITY =                                            *
*                                                                     *
*      DATE    NAME                     CHANGE                        *
*      -----   ----                     _______                       *
*                                                                     *
*    07/09/86  NICK    CODE MODIFIED TO BE REENTRANT, USED PUTLINE'S  *
*                      IN PLACE OF TPUT'S, CORRECTED CODE             *
*                                                                     *
*    05/12/2005 Frank  fix bug when only one logged on user.          *
*                                                                     *
***********************************************************************
USERS    CSECT
USERS    AMODE 31
USERS    RMODE ANY
         USING USERS,R15
         B     BEGIN
BEGINS   DS    0H
         DC    AL1(BEGIN-BEGINS)
         DC    CL8'USERS'
         DC    C'  &SYSDATE   &SYSTIME '
         DC    C'  LOUISIANA STATE UNIVERSITY'
         DC    C'  SYSTEM NETWORK COMPUTER CENTER'
         SPACE 2
*
*
**********************************************************************
*       SETUP ADDRESSABILITY TO DSECTS, SAVE REGISTERS               *
**********************************************************************
*
*
BEGIN    DS    0H
         STM   R14,R12,12(R13)    SAVE REGISTERS
         LR    R12,R15            PUT BASE ADDRESS IN R12
         DROP  R15
         USING USERS,R12          USE R12 AS BASE ADDRESS
         SPACE 2
         L     R15,CVTPTR         LOAD ADDRESS OF CVT
         USING CVTMAP,R15         ADDRESS CVT DSECT
         L     R4,CVTASVT         LOAD ADDRESS OF ASVT
         USING ASVT,R4            ADDRESS ASVT DSECT
         L     R7,ASVTMAXU        GET MAXIMUM NUMBER OF ADDRESS SPACES
         DROP  R4
         L     R2,CVTMSER         CVTMSER IS ADDRESS OF MS RES. DATA
         USING BASE,2             ADDRESS MS RESIDENT AREA
         LA    R4,10              LOAD MAXIMUM LENGTH OF A USERID
         MR    R6,R4              FIND LENGTH OF TABLE TO STORE USERIDS
         LA    R8,WORKLEN         LOAD LENGTH OF REMAINING WORK AREA
         LA    R8,0(R7,R8)        TOTAL AREA REQUIRED FOR GET MAIN
*
*
***********************************************************************
*        DO A GETMAIN TO OBTAIN STORAGE AREA FOR TASK, GETMAIN        *
*        RETURNS ADDRESS OF AREA IN R1. STORAGE IS ALLOCATED FOR      *
*        A TABLE (TO STORE USERIDS), PUTLINE AND OTHER WORK AREAS.    *
*        IT IS ASSUMED THAT THE MAX. NUMBER OF USERIDS POSSIBLE       *
*        IS EQUAL TO THE MAXIMUM NUMBER OF ADDRESS SPACES (ASVTMAXU)  *
***********************************************************************
*
GET      GETMAIN  R,LV=(8),LOC=(ANY,ANY)  ALLOCATE STORAGE TO TASK
         LR    R10,R1             R1 POINTS TO STORAGE RETURNED
         ST    R10,8(,R13)        SAVE PTR TO SAVE AREA
         ST    R13,4(,R10)        SAVE PTR TO OLD SAVE AREA
         L     R1,24(,R13)        RESTORE R1
         LR    R13,R10            LOAD MYSAVE AREA IN R13
         USING WORKAREA,R13       ADDRESS GETMAINED AREA
         LR    R11,R7             SAVE LENGTH OF TABLE
         ST    R8,MYLEN           SAVE LENGTH OF AREA TO BE FREEMAINED
         ST    R1,CPPLADDR        SAVE ADDRESS OF CPPL
*
**********************************************************************
*        INITIALIZE TABLE TO BLANKS                                  *
**********************************************************************
*
         LM    R8,R9,CLEARREG     PREPARE FOR MOVE CHARACTER LONG
         LA    R6,TABLE           LOAD ADDRESS OF AREA TO BE CLEARED
         MVCL  R6,R8              MOVE BLANK SPACES TO TABLE
*
***********************************************************************
*       GET ACTIVE TSO USERIDS FROM CSCB AND KEEP COUNT               *
***********************************************************************
*
         LA    R3,TABLE           LOAD PTR TO START OF TABLE
         LA    R5,TABLE(R11)      LOAD PTR TO END OT TABLE
         SR    R6,R6              INITIALIZE USERID COUNTER
         L     R2,BACHN           HANDLE TO CSCB CHAIN
         DROP  R2
CSCBLOOP DS    0H
         USING CHAIN,R2           ADDRESS USERS CSCB
         ICM   R2,15,CHPTR        ANY MORE USERIDS LEFT
         BZ    CSCBEND            IF NO, QUIT LOOKING
         CLI   CHTRKID,1          MAKE SURE IT IS A TSO USER
         BNE   CSCBLOOP           IF NOT, SKIP THIS ADDRESS SPACE
         CLI   8(R2),C'$'         IS THE USERID FIELD BLANK
         BL    CSCBLOOP           IF YES, SKIP THIS ADDRESS SPACE
         LA    R6,1(,R6)          BUMP USER COUNT
         MVC   0(8,R3),CHKEY      MOVE USERID INTO TABLE
         BXLE  R3,R4,CSCBLOOP     IS THE END OF THE TABLE REACHED
         MVC   MSGOUT(L'MSGOV),MSGOV  IF YES, DISPLAY OVERFLOW MESSAGE
         BAL   R14,PUTLINE
*
*
***********************************************************************
*      CONVERT USER COUNT TO DECIMAL, EDIT IT, AND DISPLAY IT         *
**********************************************************************
*
CSCBEND  DS    0H
         CVD   R6,DOUBLE         CONVERT COUNT TO DECIMAL
         MVC   COUNT(4),=X'40202020'   PREPARE FOR EDITING
         ED    COUNT(4),DOUBLE+6       EDIT, SUPPRESS ZEROS
         MVC   MSGOUT(4),COUNT MOVE COUNT TO OUTPUT AREA
         MVC   MSGOUT+4(21),NUMUSERS   MOVE TO OUTPUT AREA
         BAL   R14,PUTLINE             DISPLAY NUMBER OF USERS
         SPACE 2
*
**********************************************************************
*                  PREPARE TO SORT THE USERID TABLE                  *
**********************************************************************
*
         SR    R3,R4             BACK PTR TO LAST USER IN TABLE   FJO-1
         LR    R5,R3             SAVE PTR TO LAST USER IN TABLE
         LA    R2,TABLE          LOAD PTR TO START OF TABLE
         LR    R6,R4             LOAD LENGTH OF EACH USERID
         LR    R7,R5             LOAD PTR TO END OF TABLE
         SH    R7,=H'10'         BACK UP ONE ENTRY
         LR    R8,R2
         CR    R2,R3             Start = End ? (just one entry)   FJO-1
         BE    PRNTLOOP          Yes, nothing to sort             FJO-1
*
***********************************************************************
*                 SORT THE USERIDS                                    *
***********************************************************************
*
*
SORTLOOP DS    0H                SORT USERID'S IN TABLE
         CLC   0(8,R2),10(R2)    COMPARE TWO USERID'S
         BNH   SORTBXLE          IN SORTED ORDER, NO SWAP REQUIRED
         LR    R8,R2             SWAP THE TWO USERIDS COMPARED
         XC    0(8,R2),10(R2)
         XC    10(8,R2),0(R2)
         XC    0(8,R2),10(R2)
SORTBXLE DS    0H
         BXLE  R2,R6,SORTLOOP    END OF INNER LOOP
         LR    R7,R8
         LA    R2,TABLE          LOAD PTR TO START OF TABLE
         LR    R8,R2
COMPARE  DS    0H
         CR    R2,R7             MORE PASSES REQUIRED
         BL    SORTLOOP          END OF OUTER LOOP
*
**********************************************************************
*                 DISPLAY USERIDS, 8 PER LINE                        *
**********************************************************************
*
PRNTLOOP DS    0H
         MVC   MSGOUT(82),0(R2) MOVE 8 USERIDS TO OUTPUT AREA
         BAL   R14,PUTLINE      DISPLAY A LINE OF USERIDS
         LA    R2,80(,R2)       SET PTR TO NEXT GROUP OF 8 USERIDS
         CR    R2,R5            HAVE ALL USERIDS BEEN DISPLAYED
         BNH   PRNTLOOP         IF NOT, DISPLAY NEXT LINE         FJO-1
*
**********************************************************************
*                   FREE THE GETMAINED AREA AND EXIT                 *
**********************************************************************
*
         L     R0,MYLEN         LOAD LENGTH OF GETMAINED AREA
         LR    R1,R13           LOAD ADDRESS OF GETMAINED AREA
         L     R13,SAVEAREA+4   RESTORE SAVE AREA PTR
         FREEMAIN R,LV=(0),A=(1)  FREE AREA
         RETURN (14,12),RC=0    RETURN CODE =0
*
***********************************************************************
*                                                                     *
*      PUTLINE ROUTINE, USER MUST MOVE MESSAGE TO BE DISPLAYED IN     *
*      MSGWORK. USES R14 TO RETURN TO CALLER, ALL REGISTERS RESTORED  *
*      UPON RETURN                                                    *
***********************************************************************
*
*
PUTLINE  MVC   MSGCNTL,PUTCNTL    MOVE IN PUTLINE CONTROL CHARS
         STM   R0,R15,SAVEREGS    SAVE CALLER'S REGS
         LA    R5,MSGWORK         ADDR OF MSG BUFFER
         L     R2,CPPLADDR        ADDR OF CPPL
         USING CPPL,R2
         LA    R1,PUTLPARM        ADDR OF PLIST
         USING IOPL,R1
         MVC   IOPLECT,CPPLECT    MOVE ADDR OF ECT TO PLIST
         MVC   IOPLUPT,CPPLUPT    MOVE ADDR OF UPT TO PLIST
         LA    R0,ECB             ADDR OF ECB
         ST    R0,IOPLECB         STORE INTO PLIST
         XC    ECB,ECB            CLEAR ECB TO ZEROS
         XC    PTPB(16),PTPB      CLEAR PLIST TO ZEROS
         PUTLINE PARM=PTPB,ECB=ECB,                                    X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     X
               OUTPUT=((R5),TERM,SINGLE,DATA),                         X
               MF=(E,(1))
         MVI   MSGWORK,X'40'      CLEAR BUFFER TO SPACES
         MVC   MSGWORK+1(82),MSGWORK
         LM    R0,R15,SAVEREGS    RESTORE CALLER'S REGS
         BR    R14                RETURN TO CALLER
*
*
***********************************************************************
*                      CONSTANTS                                      *
***********************************************************************
*
*
         LTORG
NUMUSERS DC    C'  USERS LOGGED ON     '
MSGOV    DC    C'*  TABLE OVERFLOW   *'
CLEARREG DC    A(0),X'40000000'
PUTCNTL  DC    H'82'              CONTROL CHARS LENGTH + OFFSET + TEXT
         DC    H'0'
*
*
**********************************************************************
*        WORK AREA TO BE ALLOCATED DYNAMICALLY                       *
**********************************************************************
*
WORKAREA DSECT                    AREA TO BE GETMAINED
SAVEAREA DS    18F                SAVE AREA
SAVEREGS DS    16F                FOR PUTLINE ROUTINE
CPPLADDR DS    F                  SAVE AREA FOR PTR TO CPPL
MSGWORK  DS    0CL82
MSGCNTL  DS    F                  CONTROL WORD FOR PUTLINE
MSGOUT   DS    CL82               MESSAGE AREA FOR PUTLINE + 2
PUTLPARM DS    10A                SPACE FOR PULINE PARMS
ECB      DS    F
PTPB     DS    4F
         DS    0D
DOUBLE   DS    D                  USED FOR CVD
MYLEN    DS    F                  STORAGE GETMAINED
COUNT    DS    F                  NUMBER OF USERIDS
TABLE    DS    F                  AREA FOR SAVING USERIDS
WORKLEN  EQU   *-WORKAREA         SIZE OF GETMAINED AREA
         SPACE 3
         PRINT NOGEN
         SPACE 3
*        REGISTER
         YREGS
         SPACE 3
         CVT   DSECT=YES
         SPACE 3
         IKJCPPL
         SPACE 3
         IKJIOPL
         SPACE 3
         IHAASVT
         SPACE 3
         IEEBASEA
         SPACE 3
         IEECHAIN
         END   USERS
